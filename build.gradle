apply plugin: "java"
apply plugin: "idea"
apply plugin: "eclipse"

project.buildDir = "gradle/build"

repositories {
    mavenCentral()
}

ext.seleniumVersion = "2.33.0"
ext.testngVersion = "6.8.5"
ext.phantomjsdriverVersion = "1.0.3"
ext.log4jVersion = "1.2.17"
ext.gsonVersion = "2.2.4"
ext.guavaVersion = "14.0.1"

dependencies {
    compile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
    compile "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"
    compile "org.seleniumhq.selenium:selenium-server:$seleniumVersion"
    compile "log4j:log4j:$log4jVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
    compile "org.testng:testng:$testngVersion"
    compile "com.google.guava:guava:$guavaVersion"
    compile "com.github.detro.ghostdriver:phantomjsdriver:$phantomjsdriverVersion"

    testCompile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
    testCompile "org.testng:testng:$testngVersion"
}

// Source code structure used by HappyTrails
sourceSets {
    main {
        java {
            srcDir "src/java"
        }
        resources {
            srcDir "src/resources"
        }
    }
    test {
        java {
            srcDir "tst/java"
        }
        resources {
            srcDir "tst/resources"
        }
    }
}

tasks.withType(JavaExec) {
    classpath = configurations.compile + sourceSets.test.output
    args project.hasProperty("args") ? project.args.split("\\s") : []
}

test {
    // TestNG specific options
    maxParallelForks = 4
    useTestNG()

    // Listening to test execution events
    beforeTest { descriptor ->
        logger.lifecycle("Running " + descriptor)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.6"
    jarFile = "gradle/gradle-wrapper.jar"
}
